#version 450
#extension GL_ARB_separate_shader_objects : enable

layout (local_size_x_id = 1, local_size_y_id = 2) in;

layout(push_constant) uniform PushConsts
{
  int width;
  int height;
} consts;

layout (binding = 0) uniform sampler2D levelSet;
layout (binding = 1, r32f) uniform image2D levelSetBack;

void main()
{
    uvec2 localSize = gl_WorkGroupSize.xy; // Hack for Mali-GPU

    ivec2 pos = ivec2(gl_GlobalInvocationID);
    vec2 texPos = vec2((pos.x + 0.5) / consts.width, (pos.y + 0.5) / consts.height);

    float w = texture(levelSet, texPos).x;
    float wxp = textureOffset(levelSet, texPos, ivec2(1,0)).x;
    float wxn = textureOffset(levelSet, texPos, ivec2(-1,0)).x;
    float wyp = textureOffset(levelSet, texPos, ivec2(0,1)).x;
    float wyn = textureOffset(levelSet, texPos, ivec2(0,-1)).x;

    float wnew = (wxp + wxn + wyp + wyn) * 0.25;
    if (wnew < w)
    {
        imageStore(levelSetBack, pos, vec4(wnew, 0.0, 0.0, 0.0));
    }
    else
    {
        imageStore(levelSetBack, pos, vec4(w, 0.0, 0.0, 0.0));
    }
}
